let stop_words = ["the", "by", "you", "we", "to", "of", "in", "was", "a", "an", "is",
                  "and", "are", "has", "have", "had", "he", "his", "her", "she", "when",
		  "how", "why", "what", "that", "then", "now", "i", "be", "it", "as",
		  "with", "for", "but", "on", "my", "him", "so", "were", "which", "could",
		  "should", "would", "can", "shall", "will", "me", "at", "not", "they", "all",
		  "been", "from", "very", "much", "no", "yes", "their", "this", "your", "said",
		  "or", "them", "mr", "mrs", "txt", "any", "if", "in", "out", "there", "only",
                  "up", "down", "who", "one", "went", "go", "do", "done", "did", "more", "before",
                  "after", "upon", "into", "other", "these", "ye", "its", "such", "though", "yet",
                  "those", "than", "still", "about", "here", "thou", "thy", "thee", "most", "must",
                  "may"]

let delims = [\space, \newline, \,, \;, \:, \., \{, \},
              \(, \), \[, \], \', \", \?, \!]

function word_count(str)
  let (count_table = #{})
   let (add_word_count =
        ^(word)
	 when (string_length(word) > 1 && not(member(word, stop_words))) 
	   let (c = count_table[word])
	     if (c) count_table[word] = inc(c)
	     else count_table[word] = 1)
   { for_each(^(w) add_word_count(string_trim(w)), string_split(string_downcase(str), delims))
     count_table }

// TODO: print finished file names
function count_words_in_file(file_name)
  let (ct = word_count(call_with_stream(file_reader(file_name),
                       ^(s) read_all_chars(s, file_size(file_name))))
       xs = [])
  { hashtable_for_each(^(k, v) xs = (k:v):xs, ct)
    sort(xs, ^(x, y) tail(x) > tail(y)) }

function count_words_in_files(file_names)
  let (counts = map(count_words_in_file, file_names),
       count_tables = #{})
  { for_each(^(fn, c) count_tables[fn] = c, file_names, counts)
    count_tables }

function par_counter(file_names)
  spawn(^(message) | 'start -> count_words_in_files(file_names))

function split(xs)
  letseq (ln = length(xs),
          m = quotient(ln, 2))
    sublist(xs, 0, m):sublist(xs, m, ln)

function merge_tables(t01, t02)
{ hashtable_for_each(^(k, v) t01[k] = v, t02)
  t01 }

function parallel_count_words_in_files(file_names)
  letseq (parts = split(file_names),
          pc = par_counter(head(parts)),
          pcount = pc.start,
          ct01 = count_words_in_files(tail(parts)),
          ct02 = pcount(timeout = 25, default = #{}))
    { pc.quit; merge_tables(ct01, ct02) }
   
function frequent_words(count_table, n) take(n, count_table)

// sample usage:
let corpus = ["./corpus/war&peace.txt", "./corpus/mobydick.txt", "./corpus/kjbible.txt", "./corpus/pride&prejudice.txt"]

function show_most_frequent_words(corpus, n, count_fn)
  let (start_secs = now_seconds(),
       xs = count_fn(corpus))
  { showln(now_seconds() - start_secs, " secs")
    for (i = 0; i < count(corpus); inc(i))
      let (fname = corpus[i])
        showln(fname, ": ", frequent_words(xs[fname], n)) }

show_most_frequent_words(corpus, 20, count_words_in_files)
show_most_frequent_words(corpus, 20, parallel_count_words_in_files)
